options {
  STATIC = false;
}

PARSER_BEGIN(QueueMgrOptsParser)

package us.temerity.pipeline.core;

import us.temerity.pipeline.*;
import us.temerity.pipeline.parser.*;

import java.util.*;
import java.io.*;

/*------------------------------------------------------------------------------------------*/
/*   Q U E U E   M G R   O P T S   P A R S E R                                              */
/*                                                                                          */
/*    The parser for the plqueuemgr(1) queue manager daemon.                                */
/*------------------------------------------------------------------------------------------*/

class 
QueueMgrOptsParser
{
  /*----------------------------------------------------------------------------------------*/
  /*   A C C E S S                                                                          */
  /*----------------------------------------------------------------------------------------*/

  /** 
   * Set the application instance. 
   */ 
  public void 
  setApp
  (
   QueueMgrApp app  
  ) 
  {
    if(app == null) 
      throw new IllegalArgumentException("The application cannot be (null)!");
    pApp = app;
  }


  /*----------------------------------------------------------------------------------------*/
  /*   H E L P E R S                                                                        */
  /*----------------------------------------------------------------------------------------*/

  private void 
  init()
  {
    pLogBackups = 10;
  }
 


  /*----------------------------------------------------------------------------------------*/
  /*   I N T E R N A L S                                                                    */
  /*----------------------------------------------------------------------------------------*/

  /**
   * The application instance. 
   */
  private QueueMgrApp  pApp;

  /**
   * Whether to ignore existing lock files.
   */ 
  private boolean  pRebuild; 


  /*----------------------------------------------------------------------------------------*/

  /**
   * The maximum number of job servers per collection sub-thread.
   */ 
  private int  pCollectorBatchSize = 50;

  /**
   * The minimum time a cycle of the dispatcher loop should take (in milliseconds).
   */ 
  private long  pDispatcherInterval = 2000L;  /* 2-seconds */ 

  /**
   * The minimum time to wait before attempting a NFS directory attribute lookup 
   * operation after a file in the directory has been created by another host on the 
   * network (in milliseconds). 
   */ 
  private long  pNfsCacheInterval = 5000L;  /* 5-seconds */ 


  /*----------------------------------------------------------------------------------------*/

  /**
   * Whether non-information commands have been given.
   */   
  private boolean  pHasCommands; 

  /**
   * The name of the log file.
   */ 
  private Path  pLogFile; 

  /**
   * The number of backup to maintain of the log file.
   */ 
  private int  pLogBackups;

}


PARSER_END(QueueMgrOptsParser)

<DEFAULT> 
TOKEN :
{       
  /* information options */ 
  <HELP:         "--help">         
| <HTML_HELP:    "--html-help">    
| <VERSION:      "--version">      
| <RELEASE_DATE: "--release-date"> 
| <COPYRIGHT:    "--copyright">    
| <LICENSE:      "--license">    

  /* global options */	
| <REBUILD:           "--rebuild"> 
| <LOG_FILE:          "--log-file">           : PATH_SEP
| <STANDARD_LOG_FILE: "--standard-log-file"> 
| <STANDARD_LOG_DIR:  "--standard-log-dir">   : PATH_SEP
| <LOG_BACKUPS:       "--log-backups">        : INTEGER_SEP
| <LOG:               "--log">                : LOG_SEP

  /* performance tuning options */ 
| <COLLECTOR_BATCH_SIZE:     "--collector-batch-size">     : INTEGER_SEP
| <DISPATCHER_INTERVAL:      "--dispatcher-interval">      : INTEGER_SEP
| <NFS_CACHE_INTERVAL:       "--nfs-cache-interval">       : INTEGER_SEP

  /* seperators */ 
| <AE1: (["\0"])+> 

  /* errors */ 
| <UNKNOWN_OPTION:  "-" ("-")? (~["\0","="])*>  : IGNORE_STATE
| <UNKNOWN_COMMAND: ~["-","\0"] (~["\0"])*>     : IGNORE_STATE
}


/* common states */ 
<IGNORE_STATE> 
TOKEN :
{
  <IGNORED: (~[])+>
}


/* integer arguments */ 
<INTEGER_SEP> 
TOKEN :
{
  <EQ1:                   "=">   : INTEGER_STATE
| <MISSING_INTEGER_ARG: ~["="]>  : IGNORE_STATE
}

<INTEGER_STATE> 
TOKEN :
{
  <INTEGER:         (["0"-"9"])+>    : DEFAULT
| <MISSING_INTEGER: ["\0"]>          : IGNORE_STATE
| <ILLEGAL_INTEGER: (~["\0"])+>      : IGNORE_STATE
}


/* filesystem path arguments */ 
<PATH_SEP> 
TOKEN :
{
  <EQ2:                "=">   : PATH_STATE
| <MISSING_PATH_ARG: ~["="]>  : IGNORE_STATE
}

<PATH_STATE> 
TOKEN :
{
  <PATH_ARG:       ("/")* ("../")* <PATH_ARG_COMP> 
		     ("/" (".." | (<PATH_ARG_COMP>)+)?)*>      : DEFAULT
| <#PATH_ARG_COMP: (["a"-"z","A"-"Z","0"-"9","_","-","."])*>
| <MISSING_PATH:   ["\0"]>                                     : IGNORE_STATE
| <ILLEGAL_PATH:   (~["\0"])+>                                 : IGNORE_STATE
}



/* --log arguments */ 
<LOG_SEP> 
TOKEN :
{
  <EQ3:               "=">   : LOG_TYPE
| <MISSING_LOG_ARG: ~["="]>  : IGNORE_STATE
}

<LOG_TYPE> 
TOKEN :
{
  <LOG_ALL:          "all">       
| <LOG_ARG: 	     "arg">     
| <LOG_GLU: 	     "glu">        
| <LOG_OPS: 	     "ops">      
| <LOG_MEM: 	     "mem">     
| <LOG_NET: 	     "net">    
| <LOG_PLG: 	     "plg">      
| <LOG_DSP: 	     "dsp">        
| <LOG_JOB: 	     "job">        
| <LOG_COL: 	     "col">        
| <LOG_SCH: 	     "sch">   
| <LOG_SUB: 	     "sub">         
| <LOG_EXT: 	     "ext">        
| <LOG_TIM: 	     "tim">        
| <COLON:  	     ":">        : LOG_LEVEL  
| <ILLEGAL_LOG_TYPE: (~[":"])+>  : IGNORE_STATE    
}

<LOG_LEVEL> 
TOKEN : 
{
  <SEVERE:  	      "severe">  
| <WARNING: 	      "warning"> 
| <INFO:    	      "info">    
| <FINE:    	      "fine">    
| <FINER :  	      "finer">   
| <FINEST:    	      "finest">  
| <COMMA:  	      ",">             : LOG_TYPE
| <AE2:               (["\0"])+>       : DEFAULT
| <ILLEGAL_LOG_LEVEL: (~["\0",","])+>  : IGNORE_STATE
}



/*------------------------------------------------------------------------------------------*/
/*   T O P   L E V E L                                                                      */
/*------------------------------------------------------------------------------------------*/

QueueMgrServer
CommandLine() 
  throws PipelineException : 
{
  init();
}
{
  (InformationOpts() | Commands()) <EOF>
  {
    if(pHasCommands) {
      /* setup the log files */ 
      if(pLogFile != null) {
        try {
          LogMgr.getInstance().logToFile(pLogFile, pLogBackups, 10485760L);
        }
        catch(Exception ex) {
          System.out.print("Unable to setup log file: " + ex.getMessage());
          System.exit(1);
        }
      }
      
      LogMgr.getInstance().log
        (LogMgr.Kind.Net, LogMgr.Level.Info,
         "Establishing Network Connections [PluginMgr]...");
      LogMgr.getInstance().flush();
      
      /* initialize the plugins */ 
      try {
        PluginMgrClient.init();
      }
      catch(PipelineException ex) {
        LogMgr.getInstance().log
          (LogMgr.Kind.Plg, LogMgr.Level.Severe,
           ex.getMessage());
        System.exit(1);
      }
      
      /* create the server */
      return new QueueMgrServer(pRebuild, 
                                pCollectorBatchSize, pDispatcherInterval, pNfsCacheInterval);
    }
    
    return null;
  }
}


void
Commands() 
  throws PipelineException : 
{}
{
  (GlobalOpts() AE())*
  {
    pHasCommands = true;    
  }
}
    


/*------------------------------------------------------------------------------------------*/
/*   I N F O R M A T I O N   O P T I O N S                                                  */
/*------------------------------------------------------------------------------------------*/

void
InformationOpts() :
{}
{
  (Help() | HtmlHelp() | Version() | ReleaseDate() | Copyright() | License())
  {}
}

void
Help() : 
{}
{
  <HELP> AE()
  { 
    pApp.help(); 
  }
}

void
HtmlHelp() : 
{}
{
  <HTML_HELP> AE()
  { 
    pApp.htmlHelp(); 
  }
}

void
Version() :
{}
{
  <VERSION> AE()
  { 
    pApp.version(); 
  }
}

void
ReleaseDate() :
{}
{
  <RELEASE_DATE> AE()
  { 
    pApp.releaseDate(); 
  }
}

void
Copyright() :
{}
{
  <COPYRIGHT> AE() 
  { 
    pApp.copyright(); 
  }
}

void
License() :
{}
{
  <LICENSE> AE() 
  { 
    pApp.license(); 
  }
}



/*------------------------------------------------------------------------------------------*/
/*   G L O B A L   O P T I O N S                                                            */
/*------------------------------------------------------------------------------------------*/

void
GlobalOpts() 
  throws PipelineException : 
{}
{
  (Rebuild() | Log() | LogFile() | StandardLogFile() | StandardLogDir() | LogBackups() |
   CollectorBatchSize() | DispatcherInterval() | NfsCacheInterval())
  {}
}

void Rebuild() : 
{}
{
  <REBUILD>
  { 
    pRebuild = true; 
  }
}

void LogFile() : 
{
  Token t;
}
{
  <LOG_FILE> EQ() t=<PATH_ARG>
  { 
    pLogFile = new Path(t.image);
  }
}

void StandardLogDir() :
{
  Token t;
}
{
  <STANDARD_LOG_DIR> EQ() t=<PATH_ARG>
  { 
    pLogFile = new Path(new Path(t.image), "plqueuemgr.log");
  }
}

void StandardLogFile() : 
{}
{
  <STANDARD_LOG_FILE>
  { 
    pLogFile = new Path(PackageInfo.sInstPath, "/logs/plqueuemgr.log");
  }
}

void LogBackups() : 
{
  Token t;
}
{
  <LOG_BACKUPS> EQ() t=<INTEGER>
  { 
    pLogBackups = Integer.parseInt(t.image);
  }
}

void Log() : 
{}
{
  (<LOG> EQ() LoggerArg() (<COMMA> LoggerArg())*)
  {}
}

void LoggerArg() : 
{}
{
  (LoggerAll() | LoggerPair()) 
  {}
}

void LoggerAll() : 
{
  LogMgr.Level v;
}
{
  (<LOG_ALL> <COLON> v=LogLevel())
  {
    LogMgr.getInstance().setLevels(v);
  }
}

void LoggerPair() : 
{
  LogMgr.Kind k;
  LogMgr.Level v;
}
{
  (k=LogKind() <COLON> v=LogLevel())
  {
    LogMgr.getInstance().setLevel(k, v);
  }
}

LogMgr.Kind LogKind() : 
{
  LogMgr.Kind g;
}
{
  (g=ArgKind() | g=OpsKind() | g=NetKind() | g=PlgKind() | g=GluKind() | 
   g=DspKind() | g=JobKind() | g=ColKind() | g=SchKind() | g=SubKind() | 
   g=ExtKind() | g=MemKind() | g=TimKind())
  {
    return g;
  }
}

LogMgr.Kind ArgKind() : 
{}
{
  <LOG_ARG>
  {
    return LogMgr.Kind.Arg;
  }
}

LogMgr.Kind OpsKind() : 
{}
{
  <LOG_OPS>
  {
    return LogMgr.Kind.Ops;
  }
}

LogMgr.Kind MemKind() : 
{}
{
  <LOG_MEM>
  {
    return LogMgr.Kind.Mem;
  }
}

LogMgr.Kind NetKind() : 
{}
{
  <LOG_NET>
  {
    return LogMgr.Kind.Net;
  }
}

LogMgr.Kind PlgKind() : 
{}
{
  <LOG_PLG>
  {
    return LogMgr.Kind.Plg;
  }
}

LogMgr.Kind GluKind() : 
{}
{
  <LOG_GLU>
  {
    return LogMgr.Kind.Glu;
  }
}

LogMgr.Kind DspKind() : 
{}
{
  <LOG_DSP>
  {
    return LogMgr.Kind.Dsp;
  }
}

LogMgr.Kind JobKind() : 
{}
{
  <LOG_JOB>
  {
    return LogMgr.Kind.Job;
  }
}

LogMgr.Kind ColKind() : 
{}
{
  <LOG_COL>
  {
    return LogMgr.Kind.Col;
  }
}

LogMgr.Kind SchKind() : 
{}
{
  <LOG_SCH>
  {
    return LogMgr.Kind.Sch;
  }
}

LogMgr.Kind SubKind() : 
{}
{
  <LOG_SUB>
  {
    return LogMgr.Kind.Sub;
  }
}

LogMgr.Kind ExtKind() : 
{}
{
  <LOG_EXT>
  {
    return LogMgr.Kind.Ext;
  }
}

LogMgr.Kind TimKind() : 
{}
{
  <LOG_TIM>
  {
    return LogMgr.Kind.Tim;
  }
}




LogMgr.Level LogLevel() : 
{
  LogMgr.Level v;
}
{
  (v=SevereLevel() | v=WarningLevel() | v=InfoLevel() | 
   v=FineLevel() | v=FinerLevel() | v=FinestLevel()) 
  {
    return v;
  }
}

LogMgr.Level SevereLevel() :
{
  LogMgr.Level v;
}
{
  <SEVERE> 
  {
    return LogMgr.Level.Severe;
  }
}

LogMgr.Level WarningLevel() :
{
  LogMgr.Level v;
}
{
  <WARNING> 
  {
    return LogMgr.Level.Warning;
  }
}

LogMgr.Level InfoLevel() :
{
  LogMgr.Level v;
}
{
  <INFO> 
  {
    return LogMgr.Level.Info;
  }
}

LogMgr.Level FineLevel() :
{
  LogMgr.Level v;
}
{
  <FINE> 
  {
    return LogMgr.Level.Fine;
  }
}

LogMgr.Level FinerLevel() :
{
  LogMgr.Level v;
}
{
  <FINER> 
  {
    return LogMgr.Level.Finer;
  }
}

LogMgr.Level FinestLevel() :
{
  LogMgr.Level v;
}
{
  <FINEST> 
  {
    return LogMgr.Level.Finest;
  }
}


/*------------------------------------------------------------------------------------------*/

void CollectorBatchSize() 
  throws PipelineException : 
{
  Token t;
}
{
  <COLLECTOR_BATCH_SIZE> EQ() t=<INTEGER>
  {
    Integer num = Integer.parseInt(t.image);
    if(num <= 0L) 
      throw new PipelineException
        ("The collector batch size (" + num + ") must be positive!");
    pCollectorBatchSize = num;
  }
}

void DispatcherInterval()
  throws PipelineException : 
{
  Token t;
}
{
  <DISPATCHER_INTERVAL> EQ() t=<INTEGER>
  {
    Long msec = Long.parseLong(t.image);
    if(msec < 500L) 
      throw new PipelineException
        ("The dispatcher interval (" + msec + ") must be at least 500ms!");
    pDispatcherInterval = msec; 
  }
}

void NfsCacheInterval()
  throws PipelineException : 
{
  Token t;
}
{
  <NFS_CACHE_INTERVAL> EQ() t=<INTEGER>
  {
    Long msec = Long.parseLong(t.image);
    if(msec < 0L) 
      throw new PipelineException
        ("The NFS cache interval (" + msec + ") cannot be negative!");
    pNfsCacheInterval = msec; 
  }
}





/*------------------------------------------------------------------------------------------*/
/*   S H A R E D   T O K E N S                                                              */
/*------------------------------------------------------------------------------------------*/

Token
AE() :
{
  Token t;
}
{
  (t=<AE1> | t=<AE2>)
  { 
    return t; 
  }
}


Token
EQ() :
{
  Token t;
}
{
  (t=<EQ1> | t=<EQ2> | t=<EQ3>)
  { 
    return t; 
  }
}
