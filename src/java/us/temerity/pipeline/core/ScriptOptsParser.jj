options {
  STATIC = false;
}

PARSER_BEGIN(ScriptOptsParser)

package us.temerity.pipeline.core;

import us.temerity.pipeline.*;
import us.temerity.pipeline.ui.*;

import java.util.*;
import java.text.*;
import java.io.*;
import java.util.logging.*;
import javax.swing.*;        

/*------------------------------------------------------------------------------------------*/
/*   S C R I P T   O P T S   P A R S E R                                                    */
/*                                                                                          */
/*    The parser for the plscript(1) tool.                                                  */
/*------------------------------------------------------------------------------------------*/

public class 
ScriptOptsParser
{
  /*----------------------------------------------------------------------------------------*/
  /*   A C C E S S                                                                          */
  /*----------------------------------------------------------------------------------------*/

  /** 
   * Set the application instance. 
   */ 
  public void 
  setApp
  (
   ScriptApp app  
  ) 
  {
    if(app == null) 
      throw new IllegalArgumentException("The application cannot be (null)!");
    pApp = app;
  }


  /*----------------------------------------------------------------------------------------*/
  /*   I N I T I A L I Z A T I O N                                                          */
  /*----------------------------------------------------------------------------------------*/

  /**
   * Initialize the parser.
   */
  private void 
  init()
  {
    pMasterServer = PackageInfo.sMasterServer;
    pMasterPort   = PackageInfo.sMasterPort;

    pQueueServer = PackageInfo.sQueueServer; 
    pQueuePort   = PackageInfo.sQueuePort;
    pJobPort     = PackageInfo.sJobPort;

    pBatchFiles = new Stack();
  }
 
  /**
   * Initialize the parser from a parent parser.
   */ 
  public void 
  init
  (
   ScriptOptsParser parent
  ) 
  {
    pApp = parent.pApp;

    pMasterServer    = parent.pMasterServer;
    pMasterPort      = parent.pMasterPort;
    pMasterMgrClient = parent.pMasterMgrClient;

    pQueueServer    = parent.pQueueServer;
    pQueuePort      = parent.pQueuePort;
    pJobPort        = parent.pJobPort;
    pQueueMgrClient = parent.pQueueMgrClient;

    pBatchFiles = new Stack(); 
    pBatchFiles.addAll(parent.pBatchFiles);
  }


  /*----------------------------------------------------------------------------------------*/
  /*   I N T E R N A L S                                                                    */
  /*----------------------------------------------------------------------------------------*/

  /**
   * The application instance. 
   */
  private ScriptApp  pApp;


  /*----------------------------------------------------------------------------------------*/

  /**
   * The name of the host running the <B>plmaster</B><A>(1) daemon.
   */ 
  private String  pMasterServer;

  /**
   * The network port listened to by the <B>plmaster</B><A>(1) daemon.
   */
  private int  pMasterPort;

  /**
   * The connection to the <B>plmaster</B><A>(1) daemon. 
   */ 
  private MasterMgrClient  pMasterMgrClient; 


  /*----------------------------------------------------------------------------------------*/

  /*
   * The name of the host running the <B>plqueuemgr</B><A>(1) daemon. 
   */
  private String  pQueueServer;

  /**
   * The network port listened to by the <B>plqueuemgr</B><A>(1) daemon.
   */
  private int  pQueuePort;

  /**
   * The network port listened to by the <B>pljobmgr</B><A>(1) daemons.
   */
  private int  pJobPort;

  /**
   * The connection to the <B>plqueuemgr</B><A>(1) daemon. 
   */ 
  private QueueMgrClient  pQueueMgrClient; 



  /*----------------------------------------------------------------------------------------*/

  /**
   * The stack of currently open batch files.
   */ 
  private Stack pBatchFiles; 


  /*----------------------------------------------------------------------------------------*/

  /**
   * job-server command option temporaries
   */ 
  private QueueHost.Status  pJobStatus; 

  private String   pJobReserve;
  private boolean  pSetJobReserve;
  private Integer  pJobSlots;
  private TreeMap  pJobSelectionBiases;
  private TreeSet  pJobSelectionRemoves;
}


PARSER_END(ScriptOptsParser)

<DEFAULT> 
TOKEN :
{       
  /* information options */ 
  <HELP:         "--help">         
| <HTML_HELP:    "--html-help">    
| <VERSION:      "--version">      
| <RELEASE_DATE: "--release-date"> 
| <COPYRIGHT:    "--copyright">    
| <LICENSE:      "--license">    

  /* global options */	
| <MASTER_HOST:  "--master-host">   : HOST_NAME_SEP
| <MASTER_PORT:  "--master-port">   : PORT_NUMBER_SEP

| <QUEUE_HOST:   "--queue-host">    : HOST_NAME_SEP
| <QUEUE_PORT:   "--queue-port">    : PORT_NUMBER_SEP
| <JOB_PORT:     "--job-port">      : PORT_NUMBER_SEP

| <LOG:          "--log">           : LOG_SEP

  /* batch */ 
| <BATCH:        "--batch">         : PATH_SEP
		   
  /* commands */ 
| <PRIVILEGED:        "privileged">       : PRIVILEGED_OPTS 
| <ADMIN:             "admin">  
| <DEFAULT_TOOLSET:   "default-toolset">  : TOOLSET_OPTS 
| <ACTIVE_TOOLSET:    "active-toolset">   : TOOLSET_OPTS 
| <TOOLSET:           "toolset">          : TOOLSET_OPTS 
| <LICENSE_KEY:       "license-key">      : KEY_OPTS 
| <SELECTION_KEY:     "selection-key">    : KEY_OPTS  
| <JOB_SERVER:        "job-server">       : JOB_SERVER_OPTS
| <SUFFIX_EDITOR:     "suffix-editor">    : SUFFIX_EDITOR_OPTS
| <VIEW:              "view">             : VIEW_OPTS

  /* argument end separator */ 
| <AE1: ("\0")+>

  /* errors */ 
| <UNKNOWN_OPTION1:  "-" ("-")? (~["\0","="])*>  : IGNORE_STATE
| <UNKNOWN_COMMAND1: ~["-","\0"] (~["\0"])*>     : IGNORE_STATE
}


<OPTIONS> 
TOKEN :
{
  /* command options */ 
  <MSG:            "--msg">             : STRING_SEP  
| <TOTAL:          "--total">           : INTEGER_SEP  
| <SHUTDOWN:       "--shutdown">       
| <DISABLE:        "--disable">       
| <ENABLE:         "--enable">  
| <RESERVE:        "--reserve">         : USER_NAME_SEP            
| <OPEN:           "--open">  
| <SLOTS:          "--slots">           : INTEGER_SEP  
| <SELECTION_BIAS: "--selection-bias">  : KEY_BIAS_SEP  
| <REMOVE_KEY:     "--remove-key">      : KEY_NAME_SEP   
| <EDITOR:         "--editor">          : EDITOR_NAME_SEP 
| <VERSION2:       "--version">         : REVISION_NUMBER_SEP


  /* argument end separator */ 
| <AE2: ("\0")+>

  /* errors */ 
| <UNKNOWN_OPTION2:  "-" ("-")? (~["\0","="])*>  : IGNORE_STATE
| <UNKNOWN_COMMAND2: ~["-","\0"] (~["\0"])*>     : IGNORE_STATE
}


/* common states */ 
<IGNORE_STATE> 
TOKEN :
{
  <IGNORED: (~[])+>
}


/* integer arguments */ 
<INTEGER_SEP> 
TOKEN :
{
  <EQ1:                   "=">   : INTEGER_STATE
| <MISSING_INTEGER_ARG: ~["="]>  : IGNORE_STATE
}

<INTEGER_STATE> 
TOKEN :
{
  <INTEGER:         (["0"-"9"])+>    : OPTIONS
| <MISSING_INTEGER: ["\0"]>          : IGNORE_STATE
| <ILLEGAL_INTEGER: (~["\0"])+>      : IGNORE_STATE
}


/* port number arguments */ 
<PORT_NUMBER_SEP> 
TOKEN :
{
  <EQ2:                       "=">   : PORT_NUMBER_STATE
| <MISSING_PORT_NUMBER_ARG: ~["="]>  : IGNORE_STATE
}

<PORT_NUMBER_STATE> 
TOKEN :
{
  <PORT_NUMBER:         (["0"-"9"])+>    : OPTIONS
| <MISSING_PORT_NUMBER: ["\0"]>          : IGNORE_STATE
| <ILLEGAL_PORT_NUMBER: (~["\0"])+>      : IGNORE_STATE
}


/* string arguments */   
<STRING_SEP> 
TOKEN :
{
  <EQ3:                  "=">   : STRING_STATE
| <MISSING_STRING_ARG: ~["="]>  : IGNORE_STATE
}

<STRING_STATE>
TOKEN :
{
  <STRING:         (~["\0"])*>  : OPTIONS
| <EMPTY_STRING:   "\0">        : OPTIONS
}


/* filesystem path arguments */ 
<PATH_SEP> 
TOKEN :
{
  <EQ4:                "=">   : PATH_STATE
| <MISSING_PATH_ARG: ~["="]>  : IGNORE_STATE
}

<PATH_STATE> 
TOKEN :
{
  <PATH_ARG:       ("/")* ("../")* <PATH_ARG_COMP> 
		     ("/" (".." | (<PATH_ARG_COMP>)+)?)*>      : OPTIONS
| <#PATH_ARG_COMP: (["a"-"z","A"-"Z","0"-"9","_","-","."])*>
| <MISSING_PATH:   ["\0"]>                                     : IGNORE_STATE
| <ILLEGAL_PATH:   (~["\0"])+>                                 : IGNORE_STATE
}


/* hostname arguments */ 
<HOST_NAME_SEP> 
TOKEN :
{
  <EQ5:                    "=">   : HOST_NAME_STATE
| <MISSING_HOST_NAME_ARG: ~["="]>  : IGNORE_STATE
}

<HOST_NAME_STATE> 
TOKEN :
{
  <HOST_NAME:         ((["a"-"z","A"-"Z","0"-"9"])+ 
		      ("." (["a"-"z","A"-"Z","0"-"9"])+)*)>   : OPTIONS
| <MISSING_HOST_NAME: ["\0"]>                                  : IGNORE_STATE
| <ILLEGAL_HOST_NAME: (~["\0"])+>                              : IGNORE_STATE
}


/* node name arguments */ 
<NODE_SEP> 
TOKEN :
{
  <EQ6:                "=">   : NODE_STATE
| <MISSING_NODE_ARG: ~["="]>  : IGNORE_STATE
}

<NODE_STATE> 
TOKEN :
{
  <NODE_ARG:       ("/" (["a"-"z","A"-"Z","0"-"9","_","-"])+)+>  : OPTIONS
| <MISSING_NODE:   ["\0"]>                                       : IGNORE_STATE
| <ILLEGAL_NODE:   (~["\0"])+>                                   : IGNORE_STATE
}


/* toolset name arguments */ 
<TOOLSET_NAME_SEP> 
TOKEN :
{
  <EQ7:                "=">           : TOOLSET_NAME_STATE
| <MISSING_TOOLSET_NAME_ARG: ~["="]>  : IGNORE_STATE
}

<TOOLSET_NAME_STATE> 
TOKEN :
{
  <TOOLSET_NAME:           (["a"-"z","A"-"Z"]) 
		           (["a"-"z","A"-"Z","0"-"9","_","-"])+>  
| <AE3:                    ["\0"]>                                   : OPTIONS
| <ILLEGAL_TOOLSET_NAME:   (~["\0"])+>                               : IGNORE_STATE
}


/* user name arguments */ 
<USER_NAME_SEP> 
TOKEN :
{
  <EQ8:                     "=">   : USER_NAME_STATE
| <MISSING_USER_NAME_ARG: ~["="]>  : IGNORE_STATE
}

<USER_NAME_STATE> 
TOKEN :
{
  <USER_NAME:           (["a"-"z","A"-"Z"]) 
	   	          (["a"-"z","A"-"Z","0"-"9","_","-"])+>  
| <AE4:                 ["\0"]>                                   : OPTIONS
| <ILLEGAL_USER_NAME:   (~["\0"])+>                               : IGNORE_STATE
}


/* key name arguments */ 
<KEY_NAME_SEP> 
TOKEN :
{
  <EQ9:                    "=">   : KEY_NAME_STATE
| <MISSING_KEY_NAME_ARG: ~["="]>  : IGNORE_STATE
}

<KEY_NAME_STATE> 
TOKEN :
{
  <KEY_NAME:           (["a"-"z","A"-"Z"]) 
		         (["a"-"z","A"-"Z","0"-"9","_","-"])+>  
| <AE5:                ["\0"]>                                   : OPTIONS
| <ILLEGAL_KEY_NAME:   (~["\0"])+>                               : IGNORE_STATE
}


/* key bias arguments */ 
<KEY_BIAS_SEP> 
TOKEN :
{
  <EQ10:                   "=">   : KEY_BIAS_STATE
| <MISSING_KEY_BIAS_ARG: ~["="]>  : IGNORE_STATE
}

<KEY_BIAS_STATE> 
TOKEN :
{
  <KEY_BIAS_NAME:      (["a"-"z","A"-"Z"]) 
		         (["a"-"z","A"-"Z","0"-"9","_","-"])+>  
| <COLON1:             ":">      
| <KEY_BIAS:           (["0"-"9"])+>                             : OPTIONS  
| <AE6:                ["\0"]>                                   : OPTIONS
| <ILLEGAL_KEY_BIAS:   (~[":","\0"])+>                           : IGNORE_STATE
}


/* filename suffix arguments */ 
<SUFFIX_SEP> 
TOKEN :
{
  <EQ11:                 "=">   : SUFFIX_STATE
| <MISSING_SUFFIX_ARG: ~["="]>  : IGNORE_STATE
}

<SUFFIX_STATE> 
TOKEN :
{
  <SUFFIX:           (["a"-"z","A"-"Z","0"-"9"])+>  
| <AE7:              ["\0"]>                          : OPTIONS
| <ILLEGAL_SUFFIX:   (~["\0"])+>                      : IGNORE_STATE
}


/* editor arguments */ 
<EDITOR_NAME_SEP> 
TOKEN :
{
  <EQ12:                 "=">   : EDITOR_NAME_STATE
| <MISSING_EDITOR_ARG: ~["="]>  : IGNORE_STATE
}

<EDITOR_NAME_STATE> 
TOKEN :
{
  <EDITOR_NAME:      (["a"-"z","A"-"Z"]) 
	               (["a"-"z","A"-"Z","0"-"9","_","-"])+>   
| <AE8:              ["\0"]>                                  : OPTIONS
| <ILLEGAL_EDITOR:   (~["\0"])+>                              : IGNORE_STATE
}


/* revision number arguments */ 
<REVISION_NUMBER_SEP> 
TOKEN :
{
  <EQ13:                          "=">   : REVISION_NUMBER_STATE
| <MISSING_REVISION_NUMBER_ARG: ~["="]>  : IGNORE_STATE
}

<REVISION_NUMBER_STATE> 
TOKEN :
{
  <REVISION_NUMBER:            (<REVNUM> "." <REVNUM> "." <REVNUM>)>  
| <#REVNUM:                    ["1"-"9"] (["0"-"9"])*>
| <AE9:                        ["\0"]>                               : OPTIONS
| <ILLEGAL_REVISION_NUMBER:    (~["0"-"9","\0"])+>                   : IGNORE_STATE
}


/* view arguments */ 
<VIEW_NAME_SEP> 
TOKEN :
{
  <EQ14:               "=">     : VIEW_NAME_STATE
| <MISSING_VIEW_ARG: ~["="]>    : IGNORE_STATE
}

<VIEW_NAME_STATE> 
TOKEN :
{
  <VIEW_NAME:      (["a"-"z","A"-"Z"]) 
	             (["a"-"z","A"-"Z","0"-"9","_","-"])+>   
| <AE10:           ["\0"]>                                  : OPTIONS
| <ILLEGAL_VIEW:   (~["\0"])+>                              : IGNORE_STATE
}


/*------------------------------------------------------------------------------------------*/

/* --log arguments */ 
<LOG_SEP> 
TOKEN :
{
  <EQ15:              "=">   : LOG_STATE
| <MISSING_LOG_ARG: ~["="]>  : IGNORE_STATE
}

<LOG_STATE> 
TOKEN :
{
  <LOG_ALL:         "all">       
| <LOG_ARG: 	    "arg">        
| <LOG_OPS: 	    "ops">     
| <LOG_NET: 	    "net">      
| <LOG_PLG: 	    "plg">       
| <LOG_SUB: 	    "sub">     
| <COLON2:  	    ":">       
| <SEVERE:  	    "severe">  
| <WARNING: 	    "warning"> 
| <INFO:    	    "info">    
| <FINE:    	    "fine">    
| <FINER :  	    "finer">   
| <FINEST:    	    "finest">  
| <COMMA:  	    ",">       
| <AE11:            (["\0"])+>                   : OPTIONS
| <ILLEGAL_LOG_ARG: (~["\0","a"-"z",":",","])+>  : IGNORE_STATE
}


/*------------------------------------------------------------------------------------------*/

/* privileged command options */ 
<PRIVILEGED_OPTS> 
TOKEN :
{
  <PRIVILEGED_GET:    "--get">                   : OPTIONS
| <PRIVILEGED_GRANT:  "--grant">                 : USER_NAME_SEP 
| <PRIVILEGED_REVOKE: "--revoke">                : USER_NAME_SEP 
| <AE12:              ["\0"]>  
| <UNKNOWN_OPTION3:   "-" ("-")? (~["\0","="])*> : IGNORE_STATE
| <UNKNOWN3:          ~["-","\0"] (~["\0"])*>    : IGNORE_STATE         
}


/* toolset command options */ 
<TOOLSET_OPTS> 
TOKEN :
{
  <TOOLSET_GET:           "--get">                   : OPTIONS
| <TOOLSET_GET_INFO:      "--get-info">              : TOOLSET_NAME_SEP  
| <TOOLSET_GET_INFO_ALL:  "--get-info-all">          : OPTIONS
| <TOOLSET_SET:           "--set">                   : TOOLSET_NAME_SEP  
| <TOOLSET_ADD:           "--add">                   : TOOLSET_NAME_SEP 
| <TOOLSET_REMOVE:        "--remove">                : TOOLSET_NAME_SEP 
| <TOOLSET_EXPORT:        "--export">                : TOOLSET_NAME_SEP
| <AE13:                  ["\0"]>            
| <UNKNOWN_OPTION4:       "-" ("-")? (~["\0","="])*> : IGNORE_STATE
| <UNKNOWN4:              ~["-","\0"] (~["\0"])*>    : IGNORE_STATE          
}


/* key command options */ 
<KEY_OPTS> 
TOKEN :
{
  <KEY_GET:            "--get">                   : OPTIONS
| <KEY_GET_INFO:       "--get-info">              : KEY_NAME_SEP  
| <KEY_GET_INFO_ALL:   "--get-info-all">          : OPTIONS
| <KEY_ADD:            "--add">                   : KEY_NAME_SEP 
| <KEY_SET:            "--set">                   : KEY_NAME_SEP 
| <KEY_REMOVE:         "--remove">                : KEY_NAME_SEP 
| <AE14:               ["\0"]>    
| <UNKNOWN_OPTION5:    "-" ("-")? (~["\0","="])*> : IGNORE_STATE
| <UNKNOWN5:           ~["-","\0"] (~["\0"])*>    : IGNORE_STATE           
}


/* job server command options */ 
<JOB_SERVER_OPTS> 
TOKEN :
{
  <JOB_GET:            "--get">                   : OPTIONS
| <JOB_GET_INFO:       "--get-info">              : HOST_NAME_SEP  
| <JOB_GET_INFO_ALL:   "--get-info-all">          : OPTIONS
| <JOB_SET:            "--set">                   : HOST_NAME_SEP 
| <JOB_ADD:            "--add">                   : HOST_NAME_SEP 
| <JOB_REMOVE:         "--remove">                : HOST_NAME_SEP
| <AE15:               ["\0"]>   
| <UNKNOWN_OPTION6:    "-" ("-")? (~["\0","="])*> : IGNORE_STATE
| <UNKNOWN6:           ~["-","\0"] (~["\0"])*>    : IGNORE_STATE         
}


/* job server command options */ 
<SUFFIX_EDITOR_OPTS> 
TOKEN :
{
  <SUFFIX_EDITOR_GET:            "--get">                   : OPTIONS
| <SUFFIX_EDITOR_GET_INFO:       "--get-info">              : SUFFIX_SEP  
| <SUFFIX_EDITOR_GET_INFO_ALL:   "--get-info-all">          : OPTIONS
| <SUFFIX_EDITOR_SET:            "--set">                   : SUFFIX_SEP 
| <SUFFIX_EDITOR_REMOVE:         "--remove">                : SUFFIX_SEP
| <SUFFIX_EDITOR_RESET:          "--reset">                 : OPTIONS
| <AE16:                         ["\0"]>   
| <UNKNOWN_OPTION7:              "-" ("-")? (~["\0","="])*> : IGNORE_STATE
| <UNKNOWN7:                     ~["-","\0"] (~["\0"])*>    : IGNORE_STATE         
}


/* view command options */ 
<VIEW_OPTS> 
TOKEN :
{
  <VIEW_GET:            "--get">                   : OPTIONS
| <VIEW_CREATE:         "--create">                : VIEW_NAME_SEP
| <AE17:                ["\0"]>   
| <UNKNOWN_OPTION8:     "-" ("-")? (~["\0","="])*> : IGNORE_STATE
| <UNKNOWN8:            ~["-","\0"] (~["\0"])*>    : IGNORE_STATE         
}



/*------------------------------------------------------------------------------------------*/
/*   T O P   L E V E L                                                                      */
/*------------------------------------------------------------------------------------------*/

void
CommandLine() 
  throws PipelineException :
{
  init();
}
{
  (InformationOpts() | ((GlobalOpts() AE())* (Batch() | Commands()))) <EOF>
  {}
}


void 
Commands() 
  throws PipelineException :
{
  try {
    PluginMgr.init();
  }
  catch(PipelineException ex) {
    Logs.plg.severe("Unable to load plugins: " + ex);
    System.exit(1);
  }

  pMasterMgrClient = new MasterMgrClient(pMasterServer, pMasterPort);
  pQueueMgrClient  = new QueueMgrClient(pQueueServer, pQueuePort);
}
{
  (PrivilegedCmds() | AdminCmds() |
   DefaultToolsetCmds() | ActiveToolsetCmds() | ToolsetCmds() |
   LicenseKeyCmds() | SelectionKeyCmds() | JobServerCmds() | 
   SuffixEditorCmds() | ViewCmds() 

   ) 
  {
    pMasterMgrClient.disconnect();
    pQueueMgrClient.disconnect();
  }
}
    

/*------------------------------------------------------------------------------------------*/
/*   B A T C H   M O D E                                                                    */
/*------------------------------------------------------------------------------------------*/

void 
Batch() 
  throws PipelineException : 
{
  try {
    PluginMgr.init();
  }
  catch(PipelineException ex) {
    Logs.plg.severe("Unable to load plugins: " + ex);
    System.exit(1);
  }

  pMasterMgrClient = new MasterMgrClient(pMasterServer, pMasterPort);
  pQueueMgrClient  = new QueueMgrClient(pQueueServer, pQueuePort);
}
{
  (BatchOpt())
  {
    pMasterMgrClient.disconnect();
    pQueueMgrClient.disconnect();
  }
}

void 
BatchOpt() 
  throws PipelineException : 
{
  Token t;
}
{
  <BATCH> EQ() t=<PATH_ARG> AE()
  {
    /* process the batch file */ 
    File canon = null;
    {
      File file = new File(t.image);
      try {
	canon = file.getCanonicalFile();
      }
      catch(IOException ex) {
	throw new PipelineException
        ("Unable to determine the canonical filesystem path to the batch " + 
	 "file (" + file + ")!");
      }
    }

    Logs.ops.info("Processing Batch File: " + canon);

    if(pBatchFiles.contains(canon)) {
      StringBuffer buf = new StringBuffer();
      buf.append("Circular Batch Files:\n");

      Iterator iter = pBatchFiles.iterator();
      while(iter.hasNext()) {
	File file = (File) iter.next();
	buf.append("  " + file + " -> ");
      }
      buf.append("  " + canon);

      throw new PipelineException(buf.toString());
    }
    pBatchFiles.push(canon);

    ArrayList lines = pApp.packageFile(canon);
    Iterator iter = lines.iterator();
    while(iter.hasNext()) {
      String line = (String) iter.next();

      Logs.ops.info
	("\n" + 
	 pApp.repeat("*", 80) + "\n" +
	 "  BATCH: " + line.replace('\0',' ') + "\n" +
	 pApp.repeat("*", 80) + "\n");
      
      ScriptOptsParser parser = new ScriptOptsParser(new StringReader(line));
      parser.init(this);
      parser.BatchCommand();
    }

    pBatchFiles.pop();
  }
}

void 
BatchCommand() 
  throws PipelineException : 
{}
{
  (PrivilegedCmds() | AdminCmds() |
   DefaultToolsetCmds() | ActiveToolsetCmds() | ToolsetCmds() |
   LicenseKeyCmds() | SelectionKeyCmds() | JobServerCmds() | 
   SuffixEditorCmds() | ViewCmds() 
   
   ) (AE())* <EOF>
  {}
}



/*------------------------------------------------------------------------------------------*/
/*   I N F O R M A T I O N   O P T I O N S                                                  */
/*------------------------------------------------------------------------------------------*/

void
InformationOpts() :
{}
{
  (Help() | HtmlHelp() | Version() | ReleaseDate() | Copyright() | License())
  {}
}

void
Help() : 
{}
{
  <HELP> AE()
  { 
    pApp.help(); 
  }
}

void
HtmlHelp() : 
{}
{
  <HTML_HELP> AE()
  { 
    pApp.htmlHelp(); 
  }
}

void
Version() :
{}
{
  <VERSION> AE()
  { 
    pApp.version(); 
  }
}

void
ReleaseDate() :
{}
{
  <RELEASE_DATE> AE()
  { 
    pApp.releaseDate(); 
  }
}

void
Copyright() :
{}
{
  <COPYRIGHT> AE() 
  { 
    pApp.copyright(); 
  }
}

void
License() :
{}
{
  <LICENSE> AE() 
  { 
    pApp.license(); 
  }
}



/*------------------------------------------------------------------------------------------*/
/*   G L O B A L   O P T I O N S                                                            */
/*------------------------------------------------------------------------------------------*/

void
GlobalOpts() :
{}
{
  (MasterHost() | MasterPort() | 
   QueueHost() | QueuePort() | JobPort() | 
   Log())
  {}
}


void
MasterHost() :
{
  String hname = null;
}
{
  <MASTER_HOST> EQ() hname=HostName()
  { 
    pMasterServer = hname;
  }
}

void
MasterPort() :
{
  int port; 
}
{
  <MASTER_PORT> EQ() port=PortNumber() 
  { 
    pMasterPort = port;
  }
}

void
QueueHost() :
{
  String hname = null;
}
{
  <QUEUE_HOST> EQ() hname=HostName()
  { 
    pQueueServer = hname;
  }
}

void
QueuePort() :
{
  int port; 
}
{
  <QUEUE_PORT> EQ() port=PortNumber()
  { 
    pQueuePort = port;
  }
}

void
JobPort() :
{
  int port; 
}
{
  <JOB_PORT> EQ() port=PortNumber()
  { 
    pJobPort = port;
  }
}



void Log() : 
{}
{
  (<LOG> EQ() LoggerArg() (<COMMA> LoggerArg())*)
  {}
}

void LoggerArg() : 
{}
{
  (LoggerAll() | LoggerPair()) 
  {}
}

void LoggerAll() : 
{
  Level v;
}
{
  (<LOG_ALL> COLON() v=LogLevel())
  {
    Logs.arg.setLevel(v); 
    Logs.ops.setLevel(v);
    Logs.net.setLevel(v);
    Logs.plg.setLevel(v);
    Logs.sub.setLevel(v);
  }
}

void LoggerPair() : 
{
  Logger g; 
  Level v;
}
{
  (g=Logger() COLON() v=LogLevel())
  {
    g.setLevel(v);
  }
}

Logger Logger() :
{
  Logger g;
}
{
  (g=ArgLogger() | g=OpsLogger() | g=NetLogger() | 
   g=PlgLogger() | g=SubLogger())
  {
    return g;
  }
}

Logger ArgLogger() : 
{}
{
  <LOG_ARG>
  {
    return Logs.arg;
  }
}

Logger OpsLogger() : 
{}
{
  <LOG_OPS>
  {
    return Logs.ops;
  }
}

Logger NetLogger() : 
{}
{
  <LOG_NET>
  {
    return Logs.net;
  }
}

Logger PlgLogger() : 
{}
{
  <LOG_PLG>
  {
    return Logs.plg;
  }
}

Logger SubLogger() : 
{}
{
  <LOG_SUB>
  {
    return Logs.sub;
  }
}


Level LogLevel() : 
{
  Level v;
}
{
  (v=SevereLevel() | v=WarningLevel() | v=InfoLevel() | 
   v=FineLevel() | v=FinerLevel() | v=FinestLevel()) 
  {
    return v;
  }
}

Level SevereLevel() :
{
  Level v;
}
{
  <SEVERE> 
  {
    return Level.SEVERE;
  }
}

Level WarningLevel() :
{
  Level v;
}
{
  <WARNING> 
  {
    return Level.WARNING;
  }
}

Level InfoLevel() :
{
  Level v;
}
{
  <INFO> 
  {
    return Level.INFO;
  }
}

Level FineLevel() :
{
  Level v;
}
{
  <FINE> 
  {
    return Level.FINE;
  }
}

Level FinerLevel() :
{
  Level v;
}
{
  <FINER> 
  {
    return Level.FINER;
  }
}

Level FinestLevel() :
{
  Level v;
}
{
  <FINEST> 
  {
    return Level.FINEST;
  }
}


/*------------------------------------------------------------------------------------------*/
/*   P R I V I L E G E D   U S E R   C O M M A N D S                                        */
/*------------------------------------------------------------------------------------------*/

void 
PrivilegedCmds() 
  throws PipelineException : 
{}
{
  <PRIVILEGED> AE() (PrivilegedGet() | PrivilegedGrant() | PrivilegedRevoke())
  {}
}

void 
PrivilegedGet() 
  throws PipelineException : 
{}
{
  <PRIVILEGED_GET> AE()
  {
    TreeSet users = pMasterMgrClient.getPrivilegedUsers(false);
    Iterator iter = users.iterator();
    while(iter.hasNext()) {
      String user = (String) iter.next();
      Logs.ops.info(user);
    }
    Logs.flush();
  }
}

void 
PrivilegedGrant() 
  throws PipelineException : 
{
  String user = null;
}
{
  <PRIVILEGED_GRANT> EQ() user=UserName() AE()
  {
    pMasterMgrClient.grantPrivileges(user);
    Logs.ops.info("Granted privilged status to (" + user + ").");
    Logs.flush();
  }
}

void 
PrivilegedRevoke() 
  throws PipelineException : 
{
  String user = null;
}
{
  <PRIVILEGED_REVOKE> EQ() user=UserName() AE()
  {
    pMasterMgrClient.removePrivileges(user);
    Logs.ops.info("Revoked privilged status of (" + user + ").");
    Logs.flush();
  }
}


/*------------------------------------------------------------------------------------------*/
/*   A D M I N   C O M M A N D S                                                            */
/*------------------------------------------------------------------------------------------*/

void 
AdminCmds() 
  throws PipelineException : 
{}
{
  <ADMIN> AE() AdminShutdown()
  {}
}

void 
AdminShutdown() 
  throws PipelineException : 
{}
{
  <SHUTDOWN> AE()
  {
    pMasterMgrClient.shutdown();
    Logs.ops.info("Shutting Down...");
    Logs.flush();
  }
}


/*------------------------------------------------------------------------------------------*/
/*   T O O L S E T   C O M M A N D S                                                        */
/*------------------------------------------------------------------------------------------*/

void 
DefaultToolsetCmds() 
  throws PipelineException : 
{}
{
  <DEFAULT_TOOLSET> AE() (DefaultToolsetGet() |  DefaultToolsetSet())
  {}
}

void 
DefaultToolsetGet() 
  throws PipelineException : 
{}
{
  <TOOLSET_GET> AE()
  {
    Logs.ops.info(pMasterMgrClient.getDefaultToolsetName());
    Logs.flush();
  }
}

void 
DefaultToolsetSet() 
  throws PipelineException : 
{
  String tname = null;
}
{
  <TOOLSET_SET> EQ() tname=ToolsetName() AE()
  {
    pMasterMgrClient.setDefaultToolsetName(tname);
    Logs.ops.info("Default toolset set to (" + tname + ").");
    Logs.flush();
  }
}


/*------------------------------------------------------------------------------------------*/

void 
ActiveToolsetCmds() 
  throws PipelineException : 
{}
{
  <ACTIVE_TOOLSET> AE() (ActiveToolsetGet() | ActiveToolsetAdd() | ActiveToolsetRemove())
  {}
}

void 
ActiveToolsetGet() 
  throws PipelineException : 
{}
{
  <TOOLSET_GET> AE()
  {
    TreeSet tnames = pMasterMgrClient.getActiveToolsetNames(); 
    Iterator iter = tnames.iterator();
    while(iter.hasNext()) {
      String tname = (String) iter.next();
      Logs.ops.info(tname);
    }
    Logs.flush();
  }
}

void 
ActiveToolsetAdd() 
  throws PipelineException : 
{
  String tname = null;
}
{
  <TOOLSET_ADD> EQ() tname=ToolsetName() AE()
  {
    pMasterMgrClient.setToolsetActive(tname, true);
    Logs.ops.info("Toolset (" + tname + ") is now active.");
    Logs.flush();
  }
}

void 
ActiveToolsetRemove() 
  throws PipelineException : 
{
  String tname = null;
}
{
  <TOOLSET_REMOVE> EQ() tname=ToolsetName() AE()
  {
    pMasterMgrClient.setToolsetActive(tname, false);
    Logs.ops.info("Toolset (" + tname + ") is now inactive.");
    Logs.flush();
  }
}


/*------------------------------------------------------------------------------------------*/

void 
ToolsetCmds() 
  throws PipelineException : 
{}
{
  <TOOLSET> AE() (ToolsetGet() | ToolsetGetInfo() | ToolsetGetInfoAll() |  ToolsetExport())
  {}
}

void 
ToolsetGet() 
  throws PipelineException : 
{}
{
  <TOOLSET_GET> AE()
  {
    TreeSet tnames = pMasterMgrClient.getToolsetNames(); 
    Iterator iter = tnames.iterator();
    while(iter.hasNext()) {
      String tname = (String) iter.next();
      Logs.ops.info(tname);
    }
    Logs.flush();
  }
}

void 
ToolsetGetInfo() 
  throws PipelineException : 
{
  String tname = null;
}
{
  <TOOLSET_GET_INFO> EQ() tname=ToolsetName() AE()
  {
    pApp.printToolset(tname, pMasterMgrClient);
  }
}

void 
ToolsetGetInfoAll() 
  throws PipelineException : 
{}
{
  <TOOLSET_GET_INFO_ALL> AE()
  {
    pApp.printToolset(null, pMasterMgrClient);
  }
}

void 
ToolsetExport() 
  throws PipelineException : 
{
  String tname = null;
}
{
  <TOOLSET_EXPORT> EQ() tname=ToolsetName() AE()
  {
    pApp.exportToolset(tname, pMasterMgrClient);
  }
}


/*------------------------------------------------------------------------------------------*/
/*   Q U E U E   A D M I N I S T R A T I O N   C O M M A N D S                              */
/*------------------------------------------------------------------------------------------*/

void 
LicenseKeyCmds() 
  throws PipelineException : 
{}
{
  <LICENSE_KEY> AE() 
     (LicenseKeyGet() | LicenseKeyGetInfo() | LicenseKeyGetInfoAll() | 
      LicenseKeyAdd() | LicenseKeySet() | LicenseKeyRemove())
  {}
}

void 
LicenseKeyGet() 
  throws PipelineException : 
{}
{
  <KEY_GET> AE()
  {
    TreeSet knames = pQueueMgrClient.getLicenseKeyNames();
    Iterator iter = knames.iterator();
    while(iter.hasNext()) {
      String kname = (String) iter.next();
      Logs.ops.info(kname);
    }
    Logs.flush();    
  }
}

void 
LicenseKeyGetInfo() 
  throws PipelineException : 
{
  String kname = null;
}
{
  <KEY_GET_INFO> EQ() kname=KeyName() AE()
  {
    pApp.printLicenseKey(kname, pQueueMgrClient);
  }
}

void 
LicenseKeyGetInfoAll() 
  throws PipelineException : 
{}
{
  <KEY_GET_INFO_ALL> AE()
  {
    pApp.printLicenseKey(null, pQueueMgrClient);
  }
}

void 
LicenseKeyAdd() 
  throws PipelineException : 
{
  String kname = null;
  String msg = null;
  int total = 0;
}
{
  <KEY_ADD> EQ() kname=KeyName() AE() 
     ((msg=Msg() AE() total=Total()) | (total=Total() AE() msg=Msg())) AE()
  {
    LicenseKey key = new LicenseKey(kname, msg, total);
    pQueueMgrClient.addLicenseKey(key);

    Logs.ops.info("Added license key (" + kname + ").");
    Logs.flush();
  }
}
 
void 
LicenseKeySet() 
  throws PipelineException : 
{
  String kname = null;
  int total = 0;
}
{
  <KEY_SET> EQ() kname=KeyName() AE() total=Total() AE()
  {
    pQueueMgrClient.setTotalLicenses(kname, total);

    Logs.ops.info("License key (" + kname + ") now has (" + total + ") total licenses.");
    Logs.flush();
  }
}
 
void 
LicenseKeyRemove() 
  throws PipelineException : 
{
  String kname = null;
}
{
  <KEY_REMOVE> EQ() kname=KeyName() AE()
  {
    pQueueMgrClient.removeLicenseKey(kname);

    Logs.ops.info("Removed license key (" + kname + ").");
    Logs.flush();
  }
}

int 
Total() : 
{
  int total = 0;
}
{
  <TOTAL> EQ() total=IntegerLiteral() 
  {
    return total;
  }
}
      

/*------------------------------------------------------------------------------------------*/

void 
SelectionKeyCmds() 
  throws PipelineException : 
{}
{
  <SELECTION_KEY> AE() 
     (SelectionKeyGet() | SelectionKeyGetInfo() | SelectionKeyGetInfoAll() | 
      SelectionKeyAdd() | SelectionKeyRemove())
  {}
}

void 
SelectionKeyGet() 
  throws PipelineException : 
{}
{
  <KEY_GET> AE()
  {
    TreeSet knames = pQueueMgrClient.getSelectionKeyNames();
    Iterator iter = knames.iterator();
    while(iter.hasNext()) {
      String kname = (String) iter.next();
      Logs.ops.info(kname);
    }
    Logs.flush();    
  }
}

void 
SelectionKeyGetInfo() 
  throws PipelineException : 
{
  String kname = null;
}
{
  <KEY_GET_INFO> EQ() kname=KeyName() AE()
  {
    pApp.printSelectionKey(kname, pQueueMgrClient);
  }
}

void 
SelectionKeyGetInfoAll() 
  throws PipelineException : 
{}
{
  <KEY_GET_INFO_ALL> AE()
  {
    pApp.printSelectionKey(null, pQueueMgrClient);
  }
}

void 
SelectionKeyAdd() 
  throws PipelineException : 
{
  String kname = null;
  String msg = null;
}
{
  <KEY_ADD> EQ() kname=KeyName() AE() msg=Msg() AE()
  {
    SelectionKey key = new SelectionKey(kname, msg);
    pQueueMgrClient.addSelectionKey(key);
    
    Logs.ops.info("Added selection key (" + kname + ").");
    Logs.flush();
  }
}
 
void 
SelectionKeyRemove() 
  throws PipelineException : 
{
  String kname = null;
}
{
  <KEY_REMOVE> EQ() kname=KeyName() AE()
  {
    pQueueMgrClient.removeSelectionKey(kname);

    Logs.ops.info("Removed selection key (" + kname + ").");
    Logs.flush();
  }
}


/*------------------------------------------------------------------------------------------*/

void 
JobServerCmds() 
  throws PipelineException : 
{}
{
  <JOB_SERVER> AE() 
     (JobServerGet() | JobServerGetInfo() | JobServerGetInfoAll() | 
      JobServerAdd() | JobServerSet() | JobServerRemove())
  {}
}

void 
JobServerGet() 
  throws PipelineException : 
{}
{
  <JOB_GET> AE()
  {
    TreeMap hosts = pQueueMgrClient.getHosts();
    Iterator iter = hosts.keySet().iterator();
    while(iter.hasNext()) {
      String hname = (String) iter.next();
      Logs.ops.info(hname);
    }
    Logs.flush();    
  }
}

void 
JobServerGetInfo() 
  throws PipelineException : 
{
  String hname = null;
}
{
  <JOB_GET_INFO> EQ() hname=HostName() AE()
  {
    pApp.printHosts(hname, pQueueMgrClient);
  }
}

void 
JobServerGetInfoAll() 
  throws PipelineException : 
{}
{
  <JOB_GET_INFO_ALL> AE()
  {
    pApp.printHosts(null, pQueueMgrClient);
  }
}

void 
JobServerAdd() 
  throws PipelineException : 
{
  String hname = null;
}
{
  <JOB_ADD> EQ() hname=HostName() AE()
  {
    pQueueMgrClient.addHost(hname);

    Logs.ops.info("Added job server (" + hname + ").");
    Logs.flush();
  }
}

void 
JobServerSet() 
  throws PipelineException : 
{
  String hname = null;

  pJobStatus           = null;
  pJobReserve          = null;
  pSetJobReserve       = false;
  pJobSlots            = null;
  pJobSelectionBiases  = new TreeMap();
  pJobSelectionRemoves = new TreeSet();
}
{
  <JOB_SET> EQ() hname=HostName() AE() (JobServerSetOption())+
  {
    pApp.editHost(hname, 
		  pJobStatus, pJobReserve, pSetJobReserve, pJobSlots, 
		  pJobSelectionBiases, pJobSelectionRemoves, 
		  pQueueMgrClient);

    Logs.ops.info("Modified properties of job server (" + hname + ").");
    Logs.flush();
  }
}

void 
JobServerSetOption() :
{}
{
  (JobServerShutdown() | JobServerEnable() | JobServerDisable() | 
   JobServerReserve() | JobServerSlots() | 
   JobServerSelectionBias() | JobServerSelectionRemoveKey())
  {}
}

void 
JobServerShutdown() :
{}
{
  <SHUTDOWN> AE()
  {
    if(pJobStatus != null) 
      throw new ParseException
	("The --shutdown, --enable and --disable options are mutually exclusive!");
    pJobStatus = QueueHost.Status.Shutdown;
  }
}

void 
JobServerEnable() :
{}
{
  <ENABLE> AE()
  {
    if(pJobStatus != null) 
      throw new ParseException
	("The --shutdown, --enable and --disable options are mutually exclusive!");
    pJobStatus = QueueHost.Status.Enabled;
  }
}

void 
JobServerDisable() :
{}
{
  <DISABLE> AE()
  {
    if(pJobStatus != null) 
      throw new ParseException
	("The --shutdown, --enable and --disable options are mutually exclusive!");
    pJobStatus = QueueHost.Status.Disabled;
  }
}

void 
JobServerReserve() :
{
  String name    = null;
  pSetJobReserve = true;
}
{
  ((<RESERVE> EQ() name=UserName()) | <OPEN>) AE()
  {
    pJobReserve = name;
  }
}

void
JobServerSlots() : 
{
  Integer slots = null;
}
{
  <SLOTS> EQ() slots=IntegerLiteral() AE()
  {
    pJobSlots = slots;
  }
}

void 
JobServerSelectionBias() : 
{
  String kname;
  Integer bias; 
}
{
  <SELECTION_BIAS> EQ() kname=KeyBiasName() COLON() bias=KeyBias() AE()
  {
    pJobSelectionBiases.put(kname, bias);
  }  
}

void 
JobServerSelectionRemoveKey() : 
{
  String kname;
}
{
  <REMOVE_KEY> EQ() kname=KeyName() AE()
  {
    pJobSelectionRemoves.add(kname);
  }
}
  

void 
JobServerRemove() 
  throws PipelineException : 
{
  String hname = null;
}
{
  <JOB_REMOVE> EQ() hname=KeyName() AE()
  {
    pApp.removeHost(hname, pQueueMgrClient);

    Logs.ops.info("Removed job server (" + hname + ").");
    Logs.flush();
  }
}


/*------------------------------------------------------------------------------------------*/
/*   U S E R    P R E F E R E N C E S                                                       */
/*------------------------------------------------------------------------------------------*/

void 
SuffixEditorCmds() 
  throws PipelineException : 
{}
{
  <SUFFIX_EDITOR> AE() 
     (SuffixEditorGet() | SuffixEditorGetInfo() | SuffixEditorGetInfoAll() | 
      SuffixEditorSet() | SuffixEditorRemove() | SuffixEditorReset())
  {}
}

void 
SuffixEditorGet() 
  throws PipelineException : 
{}
{
  <SUFFIX_EDITOR_GET> AE()
  {
    TreeSet editors = pMasterMgrClient.getSuffixEditors();
    Iterator iter = editors.iterator();
    while(iter.hasNext()) {
      SuffixEditor se = (SuffixEditor) iter.next();
      Logs.ops.info(se.getSuffix());
    }
    Logs.flush();    
  }
}

void 
SuffixEditorGetInfo() 
  throws PipelineException : 
{
  String suffix = null;
}
{
  <SUFFIX_EDITOR_GET_INFO> EQ() suffix=Suffix() AE()
  {
    pApp.printSuffixEditor(suffix, pMasterMgrClient);
  }
}

void 
SuffixEditorGetInfoAll() 
  throws PipelineException : 
{}
{
  <SUFFIX_EDITOR_GET_INFO_ALL> AE()
  {
    pApp.printSuffixEditor(null, pMasterMgrClient);
  }
}

void 
SuffixEditorSet() 
  throws PipelineException : 
{
  String suffix = null;
  String msg = null;
  String editor = null;
}
{
  <SUFFIX_EDITOR_SET> EQ() suffix=Suffix() AE() 
     ((msg=Msg() AE() editor=Editor()) | (editor=Editor() AE() msg=Msg())) AE()
  {
    SuffixEditor se = new SuffixEditor(suffix, msg, editor);
    pApp.addSuffixEditor(se, pMasterMgrClient);

    Logs.ops.info("Added default editor for suffix (" + suffix + ").");
    Logs.flush();
  }
}
  
void 
SuffixEditorRemove() 
  throws PipelineException : 
{
  String suffix = null;
}
{
  <SUFFIX_EDITOR_REMOVE> EQ() suffix=Suffix() AE() 
  {
    pApp.removeSuffixEditor(suffix, pMasterMgrClient);

    Logs.ops.info("Removed default editor for suffix (" + suffix + ").");
    Logs.flush();
  }
}

void 
SuffixEditorReset() 
  throws PipelineException : 
{}
{
  <SUFFIX_EDITOR_RESET> AE() 
  {
    pApp.resetSuffixEditors(pMasterMgrClient);

    Logs.ops.info("Reset suffix editors to site defaults.");
    Logs.flush();
  }
}


  
/*------------------------------------------------------------------------------------------*/
/*   V I E W   C O M M A N D S                                                              */
/*------------------------------------------------------------------------------------------*/

void 
ViewCmds() 
  throws PipelineException : 
{}
{
  <VIEW> AE() 
     (ViewGet() | ViewCreate()) 
  {}
}

void 
ViewGet() 
  throws PipelineException : 
{}
{
  <VIEW_GET> AE()
  {
    TreeMap table = pMasterMgrClient.getWorkingAreas();
    TreeSet views = (TreeSet) table.get(PackageInfo.sUser);
    Iterator iter = views.iterator();
    while(iter.hasNext()) {
      String vname = (String) iter.next();
      Logs.ops.info(vname);
    }
    Logs.flush();    
  }
}

void 
ViewCreate() 
  throws PipelineException : 
{
  String vname = null;
}
{
  <VIEW_CREATE> EQ() vname=ViewName() AE()
  {
    pMasterMgrClient.createWorkingArea(PackageInfo.sUser, vname);
  }
}


/*------------------------------------------------------------------------------------------*/
/*   S H A R E D   T O K E N S                                                              */
/*------------------------------------------------------------------------------------------*/

Token
AE() :
{
  Token t;
}
{
  (t=<AE1>  | t=<AE2>  | t=<AE3>  | t=<AE4>  | t=<AE5>  | 
   t=<AE6>  | t=<AE7>  | t=<AE8>  | t=<AE9>  | t=<AE10> | 
   t=<AE11> | t=<AE12> | t=<AE13> | t=<AE14> | t=<AE15> | 
   t=<AE16> | t=<AE17>)
  { 
    return t; 
  }
}

Token
COLON() :
{
  Token t;
}
{
  (t=<COLON1> | t=<COLON2>)
  { 
    return t; 
  }
}

Token
EQ() :
{
  Token t;
}
{
  (t=<EQ1>  | t=<EQ2>  | t=<EQ3>  | t=<EQ4>  | t=<EQ5>  | 
   t=<EQ6>  | t=<EQ7>  | t=<EQ8>  | t=<EQ9>  | t=<EQ10> | 
   t=<EQ11> | t=<EQ12> | t=<EQ13> | t=<EQ14> | t=<EQ15>)
  { 
    return t; 
  }
}


/*------------------------------------------------------------------------------------------*/

String
Name() : 
{
  Token t;
}
{
  t=<USER_NAME>
  {
    return t.image;
  }
}

String
HostName() : 
{
  Token t;
}
{
  t=<HOST_NAME>
  {
    return t.image;
  }
}

String
UserName() : 
{
  Token t;
}
{
  t=<USER_NAME>
  {
    return t.image;
  }
}

String
ToolsetName() : 
{
  Token t;
}
{
  t=<TOOLSET_NAME>
  {
    return t.image;
  }
}

String
KeyName() : 
{
  Token t;
}
{
  t=<KEY_NAME>
  {
    return t.image;
  }
}

String
KeyBiasName() : 
{
  Token t;
}
{
  t=<KEY_BIAS_NAME>
  {
    return t.image;
  }
}

String
Suffix() : 
{
  Token t;
}
{
  t=<SUFFIX>
  {
    return t.image;
  }
}

String
EditorName() : 
{
  Token t;
}
{
  t=<EDITOR_NAME>
  {
    return t.image;
  }
}


String
ViewName() : 
{
  Token t;
}
{
  t=<VIEW_NAME>
  {
    return t.image;
  }
}


/*------------------------------------------------------------------------------------------*/

int
IntegerLiteral() :
{
  Token t;
}
{
  t=<INTEGER>
  { 
    try {
      return Integer.parseInt(t.image);
    }
    catch(NumberFormatException ex) {
      Logs.arg.severe(ex.getMessage());
    }
  }
}

int
PortNumber() :
{
  Token t;
}
{
  t=<PORT_NUMBER>
  { 
    try {
      return Integer.parseInt(t.image);
    }
    catch(NumberFormatException ex) {
      Logs.arg.severe(ex.getMessage());
    }
  }
}

int
KeyBias() : 
{
  Token t;
}
{
  t=<KEY_BIAS>
  { 
    try {
      return Integer.parseInt(t.image);
    }
    catch(NumberFormatException ex) {
      Logs.arg.severe(ex.getMessage());
    }
  }
}


/*------------------------------------------------------------------------------------------*/

String
StringLiteral() : 
{
  Token t;
}
{
  t=<STRING>
  { 
    if(t.image.length() > 0) 
      return t.image;
    return null;
  }
}


/*------------------------------------------------------------------------------------------*/

String 
Msg() : 
{
  String msg; 
}
{
  <MSG> EQ() msg=StringLiteral() 
  {
    return msg;
  }
}


/*------------------------------------------------------------------------------------------*/

String 
Editor() : 
{
  String editor = null;
}
{
  <EDITOR> EQ() editor=EditorName()
  {
    return editor;
  }
}
